set terminal svg background "white"
set output outputname
set xlabel "r [Bohr radius]"                  
set ylabel "probability amplitude"
#set title "Wavefunctions"
set title "`head -1 Out.Wfct.data`"

f1(x) = 2*exp(-x)*x
f2(x) = (1-x/2)*exp(-x/2)/sqrt(2)*x
f3(x) = (1-2*x/3+2*x*x/27)*exp(-x/3)*2/sqrt(27)*x

plot [0:][] \
 "Out.Wfct.data" using 1:2 every ::1 with points pointtype 1 linecolor rgb "#008080" title "n=1"\
 , "Out.Wfct.data" using 1:3 every ::1 with points pointtype 1 linecolor rgb "#800000" title "n=2"\
 , "Out.Wfct.data" using 1:4 every ::1 with points pointtype 1 linecolor rgb "#000080" title "n=3"\
 , f1(x) with lines linetype 7 linecolor rgb "#80C0C0" title "n=1 analytical"\
 , f2(x) with lines linetype 7 linecolor rgb "#C08080" title "n=2 analytical"\
 , f3(x) with lines linetype 7 linecolor rgb "#8080C0" title "n=3 analytical"